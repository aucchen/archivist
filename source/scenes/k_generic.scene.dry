title: Call K-.
subtitle: Ask her if there is anything you could do...
view-if: k_path >= 4 and daily_k = 0 and day >= k_day + k_path/2 and (day_major = 0 or day_minor = 0) and stress < stress_threshold and day < last_day
on-arrival: daily_k = 1; k_day = day; day_minor += 1; k_path += 1; stress += 1
max-choices: 1
max-visits: 5
new-page: true
tags: minor

# TODO: what if you do this three times? Missing ending

You call K-, and ask if there is anything you could do to help her (and perhaps for money), such as perhaps taking care of S-. She says yes.

- #k_generic

@k_s
title: K- has another late-night experiment...
max-visits: 1
tags: k_generic

K- has another late-night experiment tonight. She asks if you can stay at her apartment tonight until she returns, to help cook and take care of S-.

- @of_course_go: Of course, you go there to see S-.

@of_course_go

Of course, you go there to see S-. He seems to be happy to see you (or is he just pretending for your or K-'s benefit?). He hugs you with a broad smile on his face.

You make the usual rice and beans porridge. For some reason, it is easier to cook for others than it is to cook for yourself. You draw upon reserves of energy you never knew you had. As before, S- says he wants burgers, but K- told you to not indulge him this time. He grumbles a little, but seems happy to eat.

"Hey, I can pay you," says K-, after she returns. "Is $80 okay?"

- @sure_80: "Thank you."
- @keep_money: "Keep the money."

@sure_80
on-arrival: money += 80

"Thank you."



@k_household
title: K- just needs some household chores done.
max-visits: 1
tags: k_generic

K- apparently has a messy house again. She asks if you can come over to help clean up. So you go over to her apartment.

- @of_course_clean: Of course, you help her.

@of_course_clean

Of course, you help her. For whatever reason, it is easier to clean someone else's house than it is to clean your own. You draw upon reserves of energy you never knew you had for the purpose of sweeping the floor. Clumps of K-'s coarse black hair and S-'s fine hair lay strewn all over. By the time you are done, the rooms look rather more presentable.

"Hey, I can pay you," says K-. "Is $60 okay?"

- @sure_60: "Thank you."
- @keep_money: "Keep the money."

@sure_60
on-arrival: money += 60

"Thank you," you say, as K- keys in the numbers in her mobile.

"Yeah. Whatever. It's the least I could do."

@keep_money

"Keep the money."

"Oh. Well, okay." K- shrugs. "You need it more than I do, but whatever."


@k_visit_you
title: K- wants to visit you again.
view-if: k_knows_a_affair = 0 and k_encounter > 0
max-visits: 1
tags: k_generic

K- wants to visit you at your apartment again. S- is at school, and you are alone, as always. She stares at you, and touches you more than usual. You kiss, and hold each other for far too long.

"Would you like to engage in mutually beneficial physical activities for the purpose of releasing hormones to lower stress?" she asks.

- @k_visit_encounter
- @k_visit_no_encounter
- @smells_like_a
- @too_tired


@k_visit_encounter
title: The implications are clear...
subtitle: [? if cw_enabled: (CW: potential sexual content) ?] 
choose-if: a_encounter = 0 and stress < stress_threshold - 4
on-arrival: stress -= 1
set-bg: rgba(0,0,0,0)

As K- pulls you on top of her on the narrow bed, she asks, "you haven't dilated recently, have you?"

You laugh, and shake your head.

- @k_encounter_2: ...

@k_encounter_2
new-page: true

...

- @k_encounter_3: ...

@k_encounter_3

Your naked bodies are entangled on the bed, arms and legs intertwined. It is warm, so much warmer than is usual in this apartment. The heat permeates through your skin, through your bones and brain.

"You're still crying," she notices. "A lot."

Oh. You are. "It's a normal physiological reaction. Same as always."

K- sighs. "Well, I hope your physiology becomes less frightening."

"Me too."

- @k_encounter_4: You clothe yourselves.


@k_encounter_4

You clothe yourselves, and talk for a while about nothing in particular.

"Do you need any more money?" she asks. "Whatever. Here's $50. Sorry, this feels kind of weird, doing that kind of thing and then giving you money..."

- @k_visit_money_thanks: "Thank you..."
- @k_visit_no_money: "I don't need it."



@k_visit_no_encounter
title: But nothing of the sort happens.

All you do is talk for a while about nothing in particular, or rather K- talks to you, and then she leaves.

"Do you need any more money?" she asks. "Whatever. Here's $50."

- @k_visit_money_thanks: "Thank you..."
- @k_visit_no_money: "I don't need it."

@k_visit_money_thanks
on-arrival: money += 50

"Thank you..."

She keys the numbers on her mobile, and you have an additional $50.

@k_visit_no_money

"I don't need it."

"Are you sure? Well, that's too bad."



@smells_like_a
title: "You smell like A-," K- says.
view-if: a_encounter >= 1 and k_knows_a_affair = 0
on-arrival: k_knows_a_affair = 1

"Hey. You smell like A-. You've been doing stuff with him, right?"

"It's her," you say.

K- shrugs. "Whatever. I'm not jealous, but just, you know she's kind of fucked up, right? Are you sure you trust her?"

- @trust_a: Of course you trust her.
- @no_trust_a: You don't exactly trust her, but she's paying your rent.

@trust_a

Of course you trust her, and you tell K- as much.

"Well, that's your pregorative, I guess. I don't know."

@no_trust_a

You don't exactly trust A-, but she is paying your rent. You tell K- as much.

"Ha. That seems like the correct decision."

In the end, K- leaves after a while.

@too_tired
title: You're too tired for such activities.
view-if: stress >= stress_threshold - 4


@k_cook
title: K- is tired, and asks if you can cook for her.
max-visits: 1
tags: k_generic
on-arrival: food += 1

K- is tired, and asks if you can cook for her. You say yes, and visit her apartment. She has the usual ingredients, and you make the usual porridge, switching up some of the beans and rice varieties. The cooking procedes without incident. The three of you share a meal; she and S- are thankful.

- @ask_for_money: Ask for money?
- @dont_ask_for_money: Just leave.

@ask_for_money
on-arrival: stress += 1; money += 40

It feels more painful than usual, but you ask for money.

"Sorry I don't have a lot. Here's $40."

@dont_ask_for_money

You do not ask for money, or anything else, and say your goodbyes.


# k shopping
@k_shop
title: K- is tired, and asks if you can shop for her.
max-visits: 1
tags: k_generic
on-arrival: money += 150

K- is tired, and asks if you can shop for groceries for her. She sends you $100, and a list of items she wants you to purchase. Mostly it is staple foods, with a few luxury vegetables and fruits.

- @k_shop_buy: Buy all of the items on her list.
- @k_shop_abandon: What if you can't find all of the items...

@k_shop_abandon
on-arrival: money -= 50

You could likely find the items if you searched hard enough, but you do not.

"It's fine. Keep the change," she says.

@k_shop_buy
on-arrival: money -= 100

You find all of the items on the list eventually, diligently searching through multiple stores.

"Keep the change," she says.
