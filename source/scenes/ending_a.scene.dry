title: Ending: A-
go-to: a_rom if a_loved > 0; a_friend if a_loved = 0

A- looks pensive. She does not look at you, instead staring off into the distance.

# romance route

@a_rom

"I was thinking... Maybe we could start a new life."

[? if evicted = 1 : She moves closer to you on the bench, and holds your hand in hers. ?][? if evicted = 0 : She sits on the bed next to you, and holds your hand in hers. ?] "Maybe we could just run away from all of this.

- @dont_leave: "Don't leave your wife for me..."
- @family: "I don't want to break up your family...."
- @where_run_away: "Where would we run away?"

@dont_leave
go-to: a_sighs

"Don't leave your wife for me."

@family
go-to: a_sighs

"I don't want to break up your family."

@a_sighs

A- sighs. "It might as well be broken already. We've... not had the best relationship recently. We were thinking about divorce anyway."

- @rebound: "So I'm just your rebound?"
- @where_run_away: "Where would we run away?"

@rebound

"So I'm just your rebound?"

"Don't put it like that! Maybe *she* was the rebound, and you were the original. Just don't think about it too much."

- @where_run_away: "Where would we run away?"

@where_run_away

"Where would we run away? There's nowhere to go..."

"I don't know. The city is a big place. I'm sure we can find somewhere. Worst case, I'll pretend to be a man again and you can be my new girlfriend."

- @no_other_options: You have no other options but to trust her.
- @leap_of_faith: Take the leap of faith.
- @kiss_a: Kiss her, sealing the deal.
- @no_can_do: You can't do this.

@no_other_options
view-if: evicted = 1

Your other option is homelessness. "Let's do it," you say.

- @end_r: End.


@leap_of_faith

"Let's do it," you say.

She smiles, and holds you near her chest.

- @end_r: End.


@kiss_a

You kiss her, to seal the deal, or something like that.

"Does this mean a yes?"

You kiss her again. She smiles, and holds you near her chest.

- @end_r: End.


@no_can_do
choose-if: evicted = 0
unavailable-subtitle: You do not have any other options.

"No. I can't do this."

"Oh. Are you sure?"

You nod.

"Sorry, then. I'll get out of here."

- @ending_alone: End.

############################################# friend route


@a_friend

"I can keep you alive, for now."

- @what_mean: "What does that mean?"

@what_mean

"What does that mean?"

"I'll pay for an apartment for you, and give you enough to live on for a couple of months. You can buy takeout and medicine. I can help you find a job, too. But it can't be forever. And I don't think I should see you so often. It's... unsafe."

- @coward: Coward.
- @no_choices_f: You have no other options.
- @not_so_bad: This doesn't sound so bad.
- @cant_do_this: You cannot do this.

@coward

"Coward."

"Yes. I am." She sighs.

- @no_choices_f: You have no other options.
- @not_so_bad: This doesn't sound so bad.
- @cant_do_this: You cannot do this.


@no_choices_f
view-if: evicted = 1

Your other option is homelessness.

"Fine," you say.

- @end_r_2: End

@not_so_bad

This does not sound like such a bad life.

"Fine," you say.

- @end_r_2: End

@cant_do_this
choose-if: evicted = 0 and stress < stress_threshold
unavailable-subtitle: You do not have any other options.
on-arrival: a_rejected = 1

"Sorry, but, I can't do this."

A pause. "Fine," she says. "If you change your mind..."


######################################

@end_r
go-to: endings
achievement: ending_3

= End

It turns out that both of you are cowards. A- is too afraid to live publicly as a persister (who isn't these days), so she maintains a facade of masculinity to the outside world, while living a secret neofemale lifestyle within the confines of her domicile. She and her ex-wife have divorced, and now she lives with you in the most luxurious place you have ever lived, oweing to her continued career as a doctor for the city's elite. She loves you, and you love her[? if a_financial_support = 1 :, or something like that. What is love but a mutually beneficial exchange of goods and services? ?][? if a_financial_support = 0 :. ?]

**Ending 3a/9 - A-: Another world**

@end_r_2
go-to: endings
achievement: ending_3

= End

You are alive, with A- providing support. You have a new job as a data archaeologist at a medical device company. Most of the devices are at least decades old, in a constant state of refurbishment, their code locked up in various incompatible data formats, and it is your job to recover them. It is not quite as interesting as the DNA archival work, but it pays better, enough for you to afford rent and food and medicine. Your illness persists, but for now, you are alive and surviving.

**Ending 3b/9 - A-: Another world**


